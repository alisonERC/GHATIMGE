*=============================================================================
* CHP reporting for the IER extension
*=============================================================================

 PARAMETER PAR_BPTL(R,T,P);
 PARAMETER PAR_BPTM(R,T,P);
 PARAMETER PAR_CONDL(R,T,P) /EMPTY.EMPTY.EMPTY 0/;
 PARAMETER PAR_CONDM(R,T,P) /EMPTY.EMPTY.EMPTY 0/;
 PARAMETER PAR_HEATL(R,T,P) /EMPTY.EMPTY.EMPTY 0/;
 PARAMETER PAR_HEATM(R,T,P) /EMPTY.EMPTY.EMPTY 0/;
 PARAMETER ELE_CONDL(R,ALLYEAR,ALLYEAR,P,C,TS) /EMPTY.EMPTY.EMPTY.EMPTY.EMPTY.EMPTY 0/;
 PARAMETER ELE_CONDM(R,ALLYEAR,ALLYEAR,P,C,TS) /EMPTY.EMPTY.EMPTY.EMPTY.EMPTY.EMPTY 0/;
 PARAMETER ELE_BPTL(R,ALLYEAR,ALLYEAR,P,C,TS)  /EMPTY.EMPTY.EMPTY.EMPTY.EMPTY.EMPTY 0/;
 PARAMETER ELE_BPTM(R,ALLYEAR,ALLYEAR,P,C,TS)  /EMPTY.EMPTY.EMPTY.EMPTY.EMPTY.EMPTY 0/;

* PAR_ACTL(R,V,T,P,S)$ECT_CHP(R,P)                          $= PAR_ACTL(R,V,T,P,S)*ECT_INP2ELC(R,V,P);
* LOOP(ECT_CHP(R,P),PAR_ACTM(R,V,T,P,S)$PAR_ACTM(R,V,T,P,S) $= PAR_ACTM(R,V,T,P,S)/ECT_INP2ELC(R,V,P));

*---------------------------------------------------------------------
* Output of VAR_CAP
*---------------------------------------------------------------------

 PAR_BPTL(RTP(R,T,P))$ECT_CHP(R,P)  = SUM(RTP_CPTYR(R,V,T,P), ECT_INP2ELC(R,V,P)*COEF_CPT(R,V,T,P) * (VAR_NCAP.L(R,V,P)$MILESTONYR(V) + NCAP_PASTI(R,V,P)$PASTYEAR(V)));
 PAR_BPTM(RTP(R,T,P))$ECT_CHP(R,P)  = SUM(RTP_CPTYR(R,V,T,P), COEF_CPT(R,V,T,P)/ECT_INP2ELC(R,V,P)* ((VAR_NCAP.M(R,V,P)/VDA_DISC(R,V))$MILESTONYR(V)));

 PAR_CAPL(RTP(R,T,P))$ECT_CHP(R,P)  = SUM(RTP_CPTYR(R,V,T,P), ECT_INP2ELC(R,V,P) * COEF_CPT(R,V,T,P) * (VAR_NCAP.L(R,V,P)$MILESTONYR(V) + NCAP_PASTI(R,V,P)$PASTYEAR(V)));
 PAR_PASTI(RTP(R,T,P))$ECT_CHP(R,P) = SUM(RTP_CPTYR(R,V,T,P), ECT_INP2ELC(R,V,P) * COEF_CPT(R,V,T,P) * NCAP_PASTI(R,V,P)$PASTYEAR(V));
 PAR_CAPM(RTP(R,T,P))$ECT_CHP(R,P)  = SUM(RTP_CPTYR(R,V,T,P), COEF_CPT(R,V,T,P) /ECT_INP2ELC(R,V,P)* ((VAR_NCAP.M(R,V,P)/VDA_DISC(R,V))$MILESTONYR(V)));

 PAR_NCAPL(RTP(R,V,P))$ECT_CHP(R,P) = VAR_NCAP.L(R,V,P) * ECT_INP2ELC(R,V,P);
 IF(CARD(COEF_OBJINV),
  PAR_NCAPM(RTP(R,T,P))$ECT_CHP(R,P) = VAR_NCAP.M(R,T,P) /ECT_INP2ELC(R,T,P)*(SUM(RDCUR(R,CUR),(1/COEF_OBJINV(R,T,P,CUR))$COEF_OBJINV(R,T,P,CUR)));
 );

*---------------------------------------------------------------------
* ONLY for extraction condensing CHP plants
*---------------------------------------------------------------------

 PAR_CONDL(RTP(R,T,P))$ECT_CHP(R,P) = SUM(RTP_CPTYR(R,V,T,P), ECT_INP2CON(R,V,P)*COEF_CPT(R,V,T,P) * (VAR_NCAP.L(R,V,P)$MILESTONYR(V) + NCAP_PASTI(R,V,P)$PASTYEAR(V)));
 PAR_CONDM(RTP(R,T,P))$ECT_CHP(R,P) = SUM(RTP_CPTYR(R,V,T,P), COEF_CPT(R,V,T,P)/ECT_INP2CON(R,V,P) * (VAR_NCAP.M(R,V,P)$MILESTONYR(V)));
 PAR_HEATL(RTP(R,T,P))$ECT_CHP(R,P) = SUM(RTP_CPTYR(R,V,T,P), ECT_INP2DHT(R,V,P)*COEF_CPT(R,V,T,P) * (VAR_NCAP.L(R,V,P)$MILESTONYR(V) + NCAP_PASTI(R,V,P)$PASTYEAR(V)));
 PAR_HEATM(RTP(R,T,P))$ECT_CHP(R,P) = SUM(RTP_CPTYR(R,V,T,P), COEF_CPT(R,V,T,P)/ECT_INP2DHT(R,V,P) * (VAR_NCAP.M(R,V,P)$MILESTONYR(V)));

 LOOP(ECT_ELC(R,P,C),LOOP(RTP_VINTYR(R,V,T,P),ELE_BPTL(R,V,T,P,C,TS)$RTPCS_VARF(R,T,P,C,TS) $= SUM(ECT_DHT(R,P,COM), ECT_REH(R,V,P)*VAR_FLO.L(R,V,T,P,COM,TS))));
 LOOP(ECT_ELC(R,P,C),LOOP(RTP_VINTYR(R,V,T,P),ELE_BPTM(R,V,T,P,C,TS)$RTPCS_VARF(R,T,P,C,TS) $= SUM(ECT_DHT(R,P,COM), VAR_FLO.M(R,V,T,P,COM,TS)/ECT_REH(R,V,P))));

 LOOP(ECT_ELC(R,P,C),LOOP(RTP_VINTYR(R,V,T,P),ELE_CONDL(R,V,T,P,C,TS)$RTPCS_VARF(R,T,P,C,TS) $= VAR_FLO.L(R,V,T,P,C,TS) - SUM(ECT_DHT(R,P,COM), ECT_REH(R,V,P)*VAR_FLO.L(R,V,T,P,COM,TS))));
 LOOP(ECT_ELC(R,P,C),LOOP(RTP_VINTYR(R,V,T,P),ELE_CONDM(R,V,T,P,C,TS)$RTPCS_VARF(R,T,P,C,TS) $= VAR_FLO.M(R,V,T,P,C,TS) - SUM(ECT_DHT(R,P,COM), VAR_FLO.M(R,V,T,P,COM,TS)/ECT_REH(R,V,P))));
