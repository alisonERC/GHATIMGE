* putGblTS.ans

* %1        = TIMES parameter name
* %2 - %9   = TIMES arguments
* %10       = ANSWER parameter name
* %11 - %16 = ANSWER arguments
* %17       = name of TEMPSET used to speed up processing

* Put out _GLOBAL region TS parameter.

* Note that '%2' = 'OTHERYR' is assumed and hard-coded below.
* Also, as this code stands, NOT '%3' == '' is assumed.
* (If %3, %4, ..., %9 all == '' we get a compile error.)

OPTION CLEAR = %17;
LOOP(OTHERYR,
  %17(%3%4%5%6%7%8%9)$%1(OTHERYR,%3%4%5%6%7%8%9) = YES;
);
LOOP((%3%4%5%6%7%8%9)$%17(%3%4%5%6%7%8%9),
PUT / "%10";
$IF NOT '%11' == ''  PUT %11.TL;
$IF     '%11' == ''  PUT "-";
$IF NOT '%12' == ''  PUT %12.TL;
$IF     '%12' == ''  PUT "-";
$IF NOT '%13' == ''  PUT %13.TL;
$IF     '%13' == ''  PUT "-";
$IF NOT '%14' == ''  PUT %14.TL;
$IF     '%14' == ''  PUT "-";
$IF NOT '%15' == ''  PUT %15.TL;
$IF     '%15' == ''  PUT "-";
$IF NOT '%16' == ''  PUT %16.TL;
$IF     '%16' == ''  PUT "-";
LOOP(ANSOTHYR(J,OTHERYR),
  IF(%1(OTHERYR,%3%4%5%6%7%8%9),
    PUT (1*%1(OTHERYR,%3%4%5%6%7%8%9));
  ELSE
    PUT "null";
  );
);
);
